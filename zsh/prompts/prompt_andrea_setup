# Return the current path.
prompt_andrea_path() {
  echo '%F{blue}%~%f'
}

# Tell if we're in a git repository.
prompt_andrea_git_is_repo() {
  [[ -d '.git' ]] || command git rev-parse --is-inside-work-tree >/dev/null 2>&1
}

# Find the git branch we're on.
prompt_andrea_git_current_branch() {
  git symbolic-ref -q HEAD | sed -e 's|^refs/heads/||'
}

# Print a customized git prompt. This is only a wrapper around oh-my-zsh
# `git_prompt_info`, with a few addons and colors.
prompt_andrea_git_prompt() {
  ! prompt_andrea_git_is_repo && return
  echo "%F{242}($(prompt_andrea_git_current_branch))%f"
}

# Print the current kubernetes context.
prompt_andrea_kube_context() {
  KCTX=$(kubectx --current)
  exit_status=$?

  if [ "$exit_status" -eq "0" ]; then
    echo "%F{magenta}üê≥[$KCTX]%f"
  fi
}

prompt_andrea_aws_profile() {
  if [ -n "$AWS_PROFILE" ]; then
    echo "%F{magenta}üíæ[$AWS_PROFILE]%f"
  fi
}

# Returns the character that should appear before the cursor, colored based on
# the last exit status.
prompt_andrea_before_char() {
  echo '%(?.%F{magenta}.%F{red})‚Üí%f'
}

# Append a string to the prompt.
prompt_andrea_append_to_prompt() {
  PROMPT="$PROMPT""$1"
}

# Setup function.
prompt_andrea_setup() {
  PROMPT=""
  prompt_andrea_append_to_prompt '$(prompt_andrea_path) '
  prompt_andrea_append_to_prompt '$(prompt_andrea_git_prompt) '
  prompt_andrea_append_to_prompt '$(prompt_andrea_kube_context) '
  prompt_andrea_append_to_prompt '$(prompt_andrea_aws_profile) '
  prompt_andrea_append_to_prompt $'\n'
  prompt_andrea_append_to_prompt '$(prompt_andrea_before_char) '
}

# Init.
prompt_andrea_setup "$@"
