# Add an alias to zsh/extra, so that it's not version controlled and can
# be used in a workstation-specific way.
add-alias() {
  echo "alias $1=\"$2\"" >> "$DOTFILES/zsh/extra"
  source ~/.zshrc
}


# Use Ctrl-z to switch back to vim.
fancy-ctrl-z() {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line
  else
    zle push-input
    zle clear-screen
  fi
}


# Create a directory and cd into it.
md() {
  mkdir -p "$1" && cd "$1"
}


# Use git in a cooler way: 'g' for status, g {args} for normal git.
# 'compdef' ensures the same git completion works when using 'g' too.
compdef g=git
g() {
  if [[ $# > 0 ]]; then
    command git $@
  else
    command git status
  fi
}

# Refuse to ssh into a remote machine if running inside tmux. This is to avoid
# having a remote tmux session inside a local tmux session (two status bars?
# wtf?).
ssh() {
  # We're inside a tmux session.
  if [[ -n $TMUX ]]; then
    echo "First exit from the current tmux session. No one likes two status bars."
    echo "Just 'command ssh ...' if you want to f**k this."
  else
    command ssh $@
  fi
}

# Simple python server from the current directory.
server() {
  local port="${1:-8000}"
  open "http://localhost:${port}/"
  python -c $'import SimpleHTTPServer;\nmap = SimpleHTTPServer.SimpleHTTPRequestHandler.extensions_map;\nmap[""] = "text/plain";\nfor key, value in map.items():\n\tmap[key] = value + ";charset=UTF-8";\nSimpleHTTPServer.test();' "$port"
}
